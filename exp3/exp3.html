<!DOCTYPE html>	<!--表示文档类型为HTML文档-->
<html>
	<head><!--文档头部-->
		<!--使用UTF-8字符编码，无此句在某些浏览器会出现乱码-->
		<meta charset = "utf-8">
		<!--文档标题，会显示在网页标题栏-->
		<title>exp3</title>
	</head>
	
	<body><!--文档主体-->
		<!--绘图区域的id，以及宽度和高度，用WebGL绘制的图形将显示在该canvas中-->
		<canvas id="webgl" width="600" height="600">
		对不起，你的浏览器不支持HTML5的canvas元素！<!--出错时显示这句话-->
		</canvas>

		
		<!--以下为用到的shader程序和JavaScript程序，程序类型由type属性决定，
		顶点Shader程序和片元Shader程序有id，因为在JavaScript程序中需要对其
		进行访问，而JavaScript程序无id-->
		
		<!--顶点Shader程序-->
		<script id="vertex-shader" type="x-shader/x-vertex">
		attribute vec3 a_Position;	// 输入三维顶点坐标(建模坐标系)
		attribute vec3 a_Normal;	// 顶点法向(建模坐标系)

		varying vec3 v_fN;	// 法向(观察坐标系)
		varying vec3 v_fE;	// 观察向量(观察坐标系)
		varying vec3 v_fL;	// 光照向量(观察坐标系)

		uniform mat4 u_ModelView;		// 模视矩阵
		uniform mat4 u_Projection;		// 投影矩阵
		uniform vec4 u_LightPosition;	// 光源位置(观察坐标系)
		
		void main(){
			// 将顶点坐标转到观察坐标系下(在观察坐标系计算光照)
			vec3 pos = (u_ModelView * vec4(a_Position, 1.0)).xyz;
			v_fE = normalize(-pos);		// 观察者方向向量
			// 将顶点法向转到观察坐标系下(针对模视变换不含非均匀缩放情况)
			v_fN = normalize((u_ModelView * vec4(a_Normal, 0.0)).xyz);
			if(u_LightPosition.w != 0.0) // 近距离光源
				v_fL = normalize(u_LightPosition.xyz - pos);
			else					 	// 远距离光源
				v_fL = normalize(u_LightPosition.xyz); 
	
			// 裁剪坐标系下顶点坐标
			gl_Position = u_Projection * vec4(pos, 1.0); 
		}
		</script>
		
		<!--片元Shader程序-->
		<script id="fragment-shader" type="x-shader/x-fragment">
		precision mediump float;	// 浮点数精度为中等
		varying vec3 v_fN;	// 法向(观察坐标系)
		varying vec3 v_fE;	// 观察向量(观察坐标系)
		varying vec3 v_fL;	// 光照向量(观察坐标系)

		// 光源的环境光、漫反射光和镜面光分量
		uniform vec3 u_AmbientLight;
		uniform vec3 u_DiffuseLight; 
		uniform vec3 u_SpecularLight;

		// 物体材质
		uniform vec3 u_Ka;	// 环境光反射系数
		uniform vec3 u_Kd;  // 漫反射系数
		uniform vec3 u_Ks;  // 镜面反射系数
		uniform vec3 u_Ke;  // 发射系数
		uniform float u_Ns; // 高光系数
		uniform float u_d;  // 不透明度

		void main(){
			// 归一化输入的向量
			vec3 N = normalize(v_fN);
			vec3 E = normalize(v_fE);
			vec3 L = normalize(v_fL); 	// 光源方向向量(从顶点指向光源)
			vec3 H = normalize(L + E);	// 半角向量
		
			// 环境反射分量
			vec3 ambient = u_AmbientLight * u_Ka;

			// 漫反射分量
			float Fd = max(dot(L, N), 0.0);
			vec3 diffuse = Fd * u_DiffuseLight * u_Kd;
	
			// 镜面反射分量
			vec3 specular = vec3(0.0, 0.0, 0.0); // 镜面反射分量
			if( Fd != 0.0 ) {  // 即dot(L, N) > 0
				float Fs = pow(max(dot(N, H), 0.0), u_Ns);
				specular = Fs * u_SpecularLight * u_Ks; // 镜面反射分量
			} 

			// 得到最终颜色(除了上述3个分量，再加上物体自身的发射光)
			gl_FragColor.rgb = ambient + diffuse + specular + u_Ke;
			// 设置不透明度
			gl_FragColor.a = u_d;
		}
		</script>
		<!--下面是辅助我们进行程序编写的3个JavaScript程序-->
		<script type="text/javascript" src="./Common/webgl-utils.js"></script>
		<script type="text/javascript" src="./Common/initShaders.js"></script>
		<script type="text/javascript" src="./Common/MV.js"></script>
		<script type="text/javascript" src="./Common/ObjModel.js"></script>
		<!--主程序-->
		<script type="text/javascript" src="exp3.js"></script>


<!--	<p id="demo">我的第一个段落。</p>-->
<!--	<p id="demo2">我的第一个段落。</p>-->
	<p>漫反射颜色</p>
	<nobr>R</nobr>
	<input type="text" size="10" id="port11" name="port11" />
	<nobr>G</nobr>
	<input type="text" size="10" id="port12" name="port11" />
	<nobr>B</nobr>
	<input type="text" size="10" id="port13" name="port11" />
	<p>光源位置</p>
	<input type="text" size="10" id="port21" name="port11" />
	<input type="text" size="10" id="port22" name="port11" />
	<input type="text" size="10" id="port23" name="port11" />
	</body>
</html>